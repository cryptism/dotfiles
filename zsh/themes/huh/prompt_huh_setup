# Load dependencies.
#pmodload 'helper'

function prompt_huh_git_info {
  if (( _prompt_huh_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_huh_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_huh_precmd_async_data"
      unalias typeset
    fi

    # Reset PID.
    _prompt_huh_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function custom_position {
  if [[ "$1" =~ "\^0$" ]]; then
    echo $1 | sed "s/tags\/\(.*\)^0$/\1/"
  fi
  echo ""
}

function custom_commit {
  echo `echo $1 | cut -c 1-7`...
}

function prompt_huh_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_huh_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_huh_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_huh_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_huh_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_huh_git_info WINCH
  prompt_huh_precmd_async &!
  _prompt_huh_precmd_async_pid=$!
}

function prompt_huh_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_huh_precmd_async_pid=0
  _prompt_huh_precmd_async_data="${TMPPREFIX}-prompt_huh_data"

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_huh_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:dirty' format "%F{yellow}⚡%f"
  zstyle ':prezto:module:git:info:branch' format "%b"
  zstyle ':prezto:module:git:info:position' format '$(custom_position "%p")'
  zstyle ':prezto:module:git:info:commit' format '$(custom_commit "%c")'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%F{cyan}[%f%F{red}$(coalesce "%b" "%p" "%c")%f%F{cyan}]%f%D'

  dog=༼´◉◞౪◟◉༽
  communism=☭
  ctxt=⊢

  if [[ $ITERM_PROFILE == 'SUMMONING BOWLS' ]]; then
    unam='%F{cyan}(%f%F{red}%n@%m%f%F{cyan})%f '
  else
    unam=''
  fi

  PROMPT='$unam%F{green}%c%f${git_info:+${(e)git_info[prompt]}}%p%f %F{yellow}$ctxt%f '
  }

prompt_huh_setup "$@"
